import app.buildsrc.KeystoreUtil

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'org.jlleitschuh.gradle.ktlint'
    id 'com.google.firebase.appdistribution'
    id 'jacoco'
}

apply from: "$project.rootDir/buildScripts/jacoco-task.gradle"

def keystoreProperties = KeystoreUtil.buildProperties(rootProject)

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.blacklivesmatter.policebrutality"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 11
        versionName "2.4"

        resConfigs "en"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        resValue("string", "app_version_name", "v${versionName}")
        resValue("string", "maps_api_key", System.getenv("GMAP_API_KEY") ?: keystoreProperties['mapsApiKey'])
    }

    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }


    buildFeatures { // https://developer.android.com/studio/releases/gradle-plugin#buildFeatures
        dataBinding = true
    }

    buildTypes {
        release {
            // https://developer.android.com/studio/build/shrink-code
            minifyEnabled true // Enables code shrinking, obfuscation, and optimization
            shrinkResources true // Enables resource shrinking
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config

            firebaseAppDistribution {
                releaseNotes = "Latest release notes at https://github.com/amardeshbd/android-police-brutality-incidents/releases"
                groups = "lli-devs"
            }
        }
        debug {
            debug {
                testCoverageEnabled true
            }
            signingConfig signingConfigs.config
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    lintOptions { // https://developer.android.com/studio/write/lint.html
        checkAllWarnings true
        warningsAsErrors true
        ignore 'GradleDependency',
                'NewerVersionAvailable',
                'UnusedIds',
                'InvalidPackage' /* https://github.com/Kotlin/kotlinx.coroutines/issues/2004 */
    }
}

dependencies {
    // Google JetPack libs
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$rootProject.kotlinVersion"
    implementation "androidx.core:core-ktx:$rootProject.ktxVersion"
    implementation "androidx.appcompat:appcompat:$rootProject.appCompatVersion"
    implementation "com.google.android.material:material:$rootProject.materialVersion"
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.constraintLayoutVersion"
    implementation "androidx.navigation:navigation-fragment-ktx:$rootProject.navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$rootProject.navigationVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$rootProject.lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$rootProject.lifecycleVersion"
    implementation "androidx.cardview:cardview:$rootProject.cardViewVersion"
    implementation "androidx.preference:preference-ktx:$rootProject.preferenceVersion"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    // Firebase
    implementation "com.google.firebase:firebase-analytics-ktx:$rootProject.firebaseAnalyticsVersion"
    implementation "com.google.firebase:firebase-crashlytics:$rootProject.firebaseCrashlyticsVersion"

    // Work Manager
    implementation "androidx.work:work-runtime-ktx:$rootProject.workVersion"

    // Room Database
    implementation "androidx.room:room-runtime:$rootProject.roomVersion"
    implementation "androidx.room:room-ktx:$rootProject.roomVersion"
    kapt "androidx.room:room-compiler:$rootProject.roomVersion"

    // Coil - image loading
    implementation "io.coil-kt:coil:0.11.0"

    // Google Gson for JSON
    implementation "com.google.code.gson:gson:2.8.6"

    // Dagger Hilt (android dependency injection)
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    kapt "com.google.dagger:hilt-android-compiler:$hiltVersion"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:$rootProject.androidXHiltVersion"
    implementation "androidx.hilt:hilt-work:$rootProject.androidXHiltVersion"
    kapt "androidx.hilt:hilt-compiler:$rootProject.androidXHiltVersion"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$rootProject.retrofitVersion"
    implementation "com.squareup.okhttp3:okhttp:$rootProject.okhttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.okhttpVersion"

    // Timber for logging
    implementation "com.jakewharton.timber:timber:4.7.1"

    // 310BP - Backport of the Java SE 8 date-time classes
    implementation "org.threeten:threetenbp:1.4.4" // https://www.threeten.org/threetenbp/

    // debugImplementation because LeakCanary should only run in debug builds.
    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.4"

    // Google Maps SDK to show incident location
    // https://developers.google.com/maps/documentation/android-sdk/start
    // https://developers.google.com/android/guides/releases
    implementation "com.google.android.gms:play-services-maps:17.0.0"

    // Lottie - for better UX animations
    // https://github.com/airbnb/lottie-android
    implementation "com.airbnb.android:lottie:3.4.1"

    // -------------------------
    // Unit testing libraries
    // -------------------------
    testImplementation "junit:junit:$rootProject.junitVersion"
    testImplementation "androidx.test:core:1.2.0"
    testImplementation "androidx.arch.core:core-testing:2.1.0" // Test helpers for LiveData
    testImplementation "org.mockito:mockito-inline:$rootProject.mockitoVersion" // Mockito framework
    testImplementation "org.mockito:mockito-core:$rootProject.mockitoVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.7"
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation "androidx.work:work-testing:$rootProject.workVersion"
}
